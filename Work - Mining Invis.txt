//---------------------------------------------------
// Macro de Mining
// Funcoes: Recall para banco, Smeltar, Guardar, Comer, Hiding, Stealth e Minerar.
// Para GShard 2020
// ------------------------------------------
// 1- Ter UM bau no banco para guardar MINERIOS e comer FISH STEAK (opcional)
// 2- Ter uma Pickaxe e um Smith Hammer consigo ou no bau.
//
// 3- Ter um runebook com a 1ª runa para o BANCO ao lado da FORGE e a 2ª runa para a MINA.
// -----------------------------------------------------------
//
// CONFIGURACOES
//
if not listexists 'Pos_X'
  createlist 'Pos_X'
endif
@clearlist 'Pos_X'
pushlist! 'Pos_X' -1
pushlist! 'Pos_X' 0
pushlist! 'Pos_X' 1
if not listexists 'Pos_Y'
  createlist 'Pos_Y'
endif
@clearlist 'Pos_Y'
pushlist! 'Pos_Y' -1
pushlist! 'Pos_Y' 0
pushlist! 'Pos_Y' 1
if not listexists 'Directions'
  createlist 'Directions'
endif
@clearlist 'Directions'
pushlist! 'Directions' 'Southeast'
pushlist! 'Directions' 'North'
pushlist! 'Directions' 'Northwest'
pushlist! 'Directions' 'East'
pushlist! 'Directions' 'South'
pushlist! 'Directions' 'Southwest'
pushlist! 'Directions' 'West'
pushlist! 'Directions' 'Northeast'
if not listexists 'RunXY'
  createlist 'RunXY'
endif
@clearlist 'RunXY'
pushlist! 'RunXY' 'East'
pushlist! 'RunXY' 'South'
pushlist! 'RunXY' 'Southeast'
pushlist! 'RunXY' 'North'
pushlist! 'RunXY' 'Northwest'
pushlist! 'RunXY' 'Southwest'
pushlist! 'RunXY' 'West'
pushlist! 'RunXY' 'Northeast'
//
//
///////////////////////////////////////
///////// Lista de Minérios ///////////
//////////////////////////////////////
if not listexists 'Pedras'
  createlist 'Pedras'
endif
if not listexists 'Ores'
  createlist 'Ores'
endif
if not listexists 'Raros'
  createlist 'Raros'
endif
@clearlist 'Pedras'
// Pedras
pushlist 'Pedras' 0xf10 // Emerald
pushlist 'Pedras' 0xf11 // Sapphire
pushlist 'Pedras' 0xf0f // Star Sapphire
pushlist 'Pedras' 0xf18 // Tourmaline
pushlist 'Pedras' 0xf13 // Ruby
pushlist 'Pedras' 0xf15 // Citrine
pushlist 'Pedras' 0xf16 // Amethyst
pushlist 'Pedras' 0xf25 // Amber
pushlist 'Pedras' 0xf26 // Diamond
// Ores
@clearlist 'Ores'
pushlist 'Ores' 0x19b7 // Um ore
pushlist 'Ores' 0x19ba // Dois ores
pushlist 'Ores' 0x19b8 // Tres ores
pushlist 'Ores' 0x19b9 // Quatro ores
// Raros
@clearlist 'Raros'
pushlist 'Raros' 2490 // Chaos
pushlist 'Raros' 2489 // Order
pushlist 'Raros' 2492 // Infernal
pushlist 'Raros' 2493 // Celestial
//
if not @findobject 'runebookmining'
  headmsg 'Selecione o seu runebook. PRIMEIRA runa BANCO com guardas e forge ao lado e SEGUNDA runa MINA .'
  promptalias 'runebookmining'
endif
if not @findalias 'bankminerios'
  msg 'banker bank'
  headmsg 'Selecione o bau onde GUARDAR minerios e COMER Fish Steak.' '33'
  promptalias 'bankminerios'
endif
//
// Peso limite + Recall para Forge safe
//
if diffweight < 100
  while not inregion 'town' 'self'
    useobject 'runebookmining'
    waitforgump 'any' 10000
    //Gump Runebook 1
    if @gumpexists 0x472
      replygump 0x472 1
      waitforgump 0x472 15000
      replygump 0x472 101
      //Gump Runebook 2
    elseif @gumpexists 0x473
      replygump 0x473 1
      waitforgump 0x473 15000
      replygump 0x473 101
    elseif @gumpexists 0x474
      replygump 0x474 1
      waitforgump 0x474 15000
      replygump 0x474 101
    endif
    pause 11500
  endwhile
endif
//
// Recall para forge safe
//
if inregion 'town' 'self'
  togglehands 'right'
  pause 500
  //Gump 1
  if @gumpexists 0x483
    replygump 0x483 0
    //Gump 2
  elseif @gumpexists 0x484
    replygump 0x484 0
    //Gump 3
  elseif @gumpexists 0x485
    replygump 0x485 0
  endif
  @usetype 0x13e3 'any' 'self' 1
  waitforgump 'any' 10000
  replygump 'any' 0
  //
  // Smeltar
  //
  msg 'Banker Bank'
  for 0 to 'Ores'
    for 0 to 'Raros'
      while @findtype 'Ores[]' 'Raros[]' 'backpack'
        @moveitem 'found' 'bankminerios'
        pause 1000
      endwhile
    endfor
    while @findtype 'Ores[]' 'any' 'backpack'
      @useobject 'found'
      pause 1000
    endwhile
  endfor
endif
//
// Guardar
//
if inregion 'town' 'self'
  Resync
  pause 1000
  headmsg 'Guardando minerios'
  msg 'banker bank'
  pause 1000
  @clearjournal
  for 0 to 'Pedras'
    if @findtype 'Pedras[]' 'backpack'
      @moveitem 'found' 'bankminerios'
      pause 1000
    endif
  endfor
  for 0 to 'Ores'
    if @findtype 'Ores[]' 'backpack'
      @moveitem 'found' 'bankminerios'
      pause 1000
    endif
  endfor
  while @movetype 0x1bf2 'backpack' 'bankminerios'
    pause 1000
  endwhile
  //
  // Restock
  //
  if not @findtype 0x13e3 'any' 'backpack' 'any' 0
    useobject 'bankminerios'
    pause 500
    @movetype 0x13e3 'bankminerios' 'backpack' 0 0 0 'any'
    pause 1000
  endif
  if @findtype 0x97b 'any' 'bankminerios'
    useobject 'found'
  else
    headmsg '*Sem fish steak*
  endif
  //
  // Recall para a Mina
  //
  while mana < 40
    useskill 'meditation'
    pause 3150
  endwhile
  if inregion 'town' 'self'
    @clearjournal
    pause 500
    useobject 'runebookmining'
    waitforgump 'any' 15000
    //Gump Runebook 1
    if @gumpexists 0x472
      replygump 0x472 2
      waitforgump 0x472 15000
      replygump 0x472 102
      //Gump Runebook 2
    elseif @gumpexists 0x473
      replygump 0x473 2
      waitforgump 0x473 15000
      replygump 0x473 102
    elseif @gumpexists 0x474
      replygump 0x474 2
      waitforgump 0x474 15000
      replygump 0x474 102
    endif
    pause 11500
    if @findtype '0xe85' 'any' or @findtype '0xe86' 'any'
      @useobject 'found'
    endif
    pause 750
  endif
endif
@cancelautotarget
for 0 to 2
  for 0 to 'RunXY'
    for 0 to 'Directions'
      for 0 to 'Pos_X'
        for 0 to 'Pos_Y'
          if not hidden 'self'
            while not hidden 'self'
              useskill 'Hiding'
              pause 3500
            endwhile
            pause 750
            if hidden 'self'
              useskill 'Stealth'
              while not @injournal 'mover em silencio' 'system'
              endwhile
            else
              while not hidden 'self'
                useskill 'Hiding'
                pause 3500
              endwhile
            endif
          endif
          //
          // Minerar em si
          //
          @unsetalias 'quit'
          while not @findalias 'quit'
            @cancelautotarget
            if dead 'self'
              replay
            endif
            autotargettileoffset! 'Pos_X[]' 'Pos_Y[]' 0
            if not @useobject! 'righthand'
              if @findtype '0xe85' 'any' or @findtype '0xe86' 'any'
                @useobject 'found'
              endif
            endif
            clearjournal
            @settimer 't_timeout' 0
            while @timer 't_timeout' < 15000
              //
              //  Jornal
              //
              if @injournal 'Voce pos' 'system' or @injournal 'Voce nao encontrou' 'system'
                break
              elseif @injournal 'Nao ha nada aqui' 'system'
                @setalias 'quit' 'self'
                break
              elseif @injournal 'Voce nao tem uma' 'system' or @injournal 'You cannot mine' 'system'
                @setalias 'quit' 'self'
                break
              elseif @injournal 'sorte' 'system'
                stop
              endif
            endwhile
            if diffweight < 100
              replay
            endif
          endwhile
        endfor
      endfor
      for 4
        walk 'Directions[]'
      endfor
    endfor
    for 8
      walk 'RunXY[]'
    endfor
  endfor
endfor
// by tork
